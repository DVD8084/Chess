import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
}

version '0.2.5-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'

application {
    mainClassName = 'com.syntech.chess.Main'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.syntech.chess.Main'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

ext {
    lwjglVersion = '3.2.3'
    imguiVersion = '1.79-1.4.0'
}

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.imguiNatives = "imgui-java-natives-linux"
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.imguiNatives = "imgui-java-natives-macos"
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.imguiNatives = System.getProperty("os.arch").contains("64") ? "imgui-java-natives-windows" : "imgui-java-natives-windows-x86"
        project.ext.lwjglNatives = System.getProperty("os.arch").contains("64") ? "natives-windows" : "natives-windows-x86"
        break
}

dependencies {
    implementation "io.imgui.java:imgui-java-binding:$imguiVersion"
    implementation "io.imgui.java:imgui-java-lwjgl3:$imguiVersion"
    runtimeOnly "io.imgui.java:$imguiNatives:$imguiVersion"

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    ['', '-opengl', '-glfw'].each {
        implementation "org.lwjgl:lwjgl$it:$lwjglVersion"
        runtimeOnly "org.lwjgl:lwjgl$it::$lwjglNatives"
    }

    compile 'com.fasterxml.jackson.core:jackson-core:2.10.1'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.10.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.10.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.0.1'
    compile 'org.jetbrains:annotations:16.0.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

run {
    // jvmArgs ""
}
